// https://github.com/beryx/fxgl-sliding-puzzle/blob/master/.github/workflows/gradle.yml

plugins {
    id 'java'
    id 'java-library'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'org.beryx.runtime' version '1.13.1'
}

group 'org.example'
version '2.6.5'

application {
    mainClassName = "fqlite.base.JARStarter"
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs "resources"
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
}


javafx {
    version = "21"
    modules = [
            'javafx.base',
            'javafx.controls',
            'javafx.swing',
            'javafx.web',
            'javafx.fxml',
            'javafx.graphics',
            'javafx.media'
    ]
}

jar {
    manifest {
        attributes(
                'Main-Class': 'fqlite.base.JARStarter'
        )
    }
}

/*
def platformOverride = "mac";
if (project.hasProperty("platform")) {
    platformOverride = project.getProperty("platform")
    println "Target platform set to $platformOverride"
}
*/

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

// Uncomment and adjust the code below if you want to generate images for multiple platforms.
// (You need to also uncomment the line 'targetPlatformName = ...' in the jpackage block.)
/*
    if (platformOverride == "lin") {
        targetPlatform("lin") {
            jdkHome = jdkDownload("https://download.java.net/java/GA/jdk20.0.2/6e380f22cbe7469fa75fb448bd903d8e/9/GPL/openjdk-20.0.2_linux-x64_bin.tar.gz")
        }
    }
    else if (platformOverride == "mac") {
        targetPlatform("mac") {
            jdkHome = jdkDownload("https://download.java.net/java/GA/jdk20.0.2/6e380f22cbe7469fa75fb448bd903d8e/9/GPL/openjdk-20.0.2_macos-x64_bin.tar.gz") {
                archiveExtension = "tar.gz"
                pathToHome = "jdk-20.0.2.jdk/Contents/Home"
                overwrite = true
            }
        }
    } else if (platformOverride == "win") {
        targetPlatform("win") {
            jdkHome = jdkDownload("https://download.java.net/java/GA/jdk20.0.2/6e380f22cbe7469fa75fb448bd903d8e/9/GPL/openjdk-20.0.2_windows-x64_bin.zip")
        }
    }
*/
    launcher {
        noConsole = true
    }

    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        installerOptions += ['--resource-dir', "resources"]

        if (currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        } else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', 'fqlite', '--linux-shortcut']
        } else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', 'fqlite']
        }
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

dependencies {
    implementation group: 'org.antlr', name: 'antlr4', version: '4.8'
    implementation group: 'org.apache.avro', name: 'avro', version: '1.11.1'
    implementation group: 'nl.pvanassen', name: 'bplist', version: '1.0.0'
    implementation group: 'org.mongodb', name: 'bson', version: '3.6.0'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    implementation group: 'commons-io', name: 'commons-io', version: '2.14.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.13.0'
    implementation group: 'commons-logging', name: 'commons-logging', version: '1.2'
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.1.2'
    implementation group: 'fr.brouillard.oss', name: 'cssfx', version: '11.5.1'
    implementation group: 'org.apache.pdfbox', name: 'fontbox', version: '3.0.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-core', version: '12.3.1'
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-javafx', version: '12.3.1'
    implementation group: 'org.kordamp.ikonli', name: 'ikonli-materialdesign-pack', version: '12.3.1'
    implementation group: 'tech.units', name: 'indriya', version: '2.1.2'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.16.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.16.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.15.2'
    implementation group: 'javax.inject', name: 'javax.inject', version: '1'
    implementation group: 'org.msgpack', name: 'msgpack-core', version: '0.9.0'
    implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '3.0.0'
    implementation group: 'org.apache.pdfbox', name: 'pdfbox-io', version: '3.0.0'
    implementation group: 'com.dlsc.pdfviewfx', name: 'pdfviewfx', version: '3.0.0'
    implementation group: 'trust.nccgroup', name: 'readablethrift', version: '1.0.0'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.9'
    implementation group: 'javax.measure', name: 'unit-api', version: '2.2'
    implementation group: 'com.dlsc.unitfx', name: 'unitfx', version: '1.0.10'
    implementation group: 'tech.uom.lib', name: 'uom-lib-common', version: '2.1'

    //implementation group: 'org.openjfx', name: 'javafx-base', version: '17.0.8'
    //implementation group: 'org.openjfx', name: 'javafx-controls', version: '17.0.8'
    //implementation group: 'org.openjfx', name: 'javafx-swing', version: '17.0.8'
    //implementation group: 'org.openjfx', name: 'javafx-web', version: '17.0.8'
    //implementation group: 'org.openjfx', name: 'javafx-fxml', version: '17.0.8'
    //implementation group: 'org.openjfx', name: 'javafx-graphics', version: '17.0.8'
    //implementation group: 'org.openjfx', name: 'javafx-media', version: '17.0.8'

    implementation files('lib/FxTextEditor.jar')
    implementation files('lib/SerializationDumper-v1.13.jar')
    implementation files('lib/javafx-swt.jar')

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
}